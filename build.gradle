plugins {
    id 'java'
    id 'checkstyle'
}

ext {
    moduleName = 'se.lovebrandefelt.graphingcalculator'

    jUnitGroup = 'org.junit.jupiter'
    jUnitVersion = '5.2.0'

    checkstyleVersion = '8.11'
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: jUnitGroup, name: 'junit-jupiter-api', version: jUnitVersion
    testRuntime group: jUnitGroup, name: 'junit-jupiter-engine', version: jUnitVersion
}

test {
    useJUnitPlatform()
}

allprojects {
    version '1.0-SNAPSHOT'

    sourceCompatibility = 1.10

    afterEvaluate {
        repositories {
            jcenter()
        }

        compileJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                ]
                classpath = files()
            }
        }
    }
}

checkstyle {
    toolVersion checkstyleVersion
}

tasks.withType(Checkstyle) {
    exclude 'module-info.java'
}
